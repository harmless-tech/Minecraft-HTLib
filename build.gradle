import java.awt.Desktop

plugins {
	id "fabric-loom" version "0.5-SNAPSHOT"
	id "maven-publish"

	id "idea"
	id "de.fuerstenau.buildconfig" version "1.1.8"
	id "com.jfrog.bintray" version "1.8.5"
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

configurations {
	fatjar

	implemention.extendsFrom fatJar
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	// Annotations
	fatJar "org.atteo.classindex:classindex:3.4"
	implementation "org.atteo.classindex:classindex:3.4"
	annotationProcessor "org.atteo.classindex:classindex:3.4"

	compileOnly 'org.projectlombok:lombok:1.18.16'
	annotationProcessor 'org.projectlombok:lombok:1.18.16'
}

buildConfig {
	appName = project.name
	version = project.version

	clsName = "BuildConfig"
	packageName = project.group
	charset = "UTF-8"
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"

	def targetVersion = 8
	if (JavaVersion.current().isJava9Compatible()) {
		 it.options.release = targetVersion
	}
}

java {
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
	from {
		configurations.fatJar.collect { it.isDirectory() ? it : zipTree(it) }
	}
}

task openDoc {
	dependsOn javadoc

	doLast {
		File file = new File("$buildDir/docs/javadoc/index.html")
		Desktop.desktop.browse(file.toURI())
	}
}
task doc { dependsOn openDoc }

task javadocJar(type: Jar) {
	from javadoc
	archiveClassifier = "javadoc"
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			groupId = project.group
			artifactId = project.archivesBaseName
			version = project.version

			artifact(remapJar) {
				builtBy remapJar
			}
			artifact(sourcesJar) {
				builtBy remapSourcesJar
			}
			artifact(javadocJar) {
				builtBy javadocJar
			}
		}
	}
}

bintray {
	user = System.getenv("BINTRAY_USER")
	key = System.getenv("BINTRAY_API_KEY")
	publications = ["mavenJava"]
	pkg {
		repo = "m2"
		userOrg = System.getenv("BINTRAY_USER")
		name = project.archivesBaseName
		licenses = ["MIT"]
		vcsUrl = "https://github.com/harmless-tech/minecraft-htlib"
		version {
			name = project.version
			released = new Date()
			vcsTag = "v" + project.version
		}
	}
}
